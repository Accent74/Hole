<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
	<head>
		<meta content="True" name="vs_showGrid">
		<meta http-equiv="content-type" content="text/html; charset=windows-1251">
		<LINK href="default.css" type="text/css" rel="stylesheet">
			<SCRIPT language="vbScript">
			
Dim wa
Dim HTMLPages
Dim repDict
Dim IsLoaded
	
Set repDict = CreateObject("Scripting.Dictionary")
HTMLPages = Array("folders.htm", "accounts.htm", "agents.htm", "entities.htm", "miscs.htm", "binders.htm", "templates.htm")
IsLoaded = False

function window_onload()
	Dim Name
	
	set wa = window.external.Application.Workarea
	If wa.site is nothing then exit function
	
	With wa.site

		If .Kind <> 5 then window.document.location.assign HTMLPages(.Kind - 1)
		
		IF Not wa.map.exists("tblPropsVisible") then wa.map("tblPropsVisible") = 11
		IF Not wa.map.exists("tblReportsVisible") then wa.map("tblReportsVisible") = 31
		
		showtable(wa.map("tblPropsVisible"))
		showtable(wa.map("tblReportsVisible"))
		
		Select Case .Type
			Case -1
				MscType.InnerHTML = "Тип аналитики"
			Case 0
				MscType.InnerHTML = "Папка аналитики"
			Case 1
				MscType.InnerHTML = "Элемент аналитики"
		End Select
		
		If .ID = 0 then
			Name = "Корень аналитики"
		Else
			If .IsShortcut then 
				Name = .Name & "(ярлык)"
			Else
				Name = .Name 
			end IF
		end if
		
		If Not IsLoaded and .id <> 0 then
			IsLoaded = true
			Loadreports .repelems
			LoadProps .ID
		End if			
		
	end With
	
	MscName.InnerHTML = Name
	
End function

sub LoadReports(repElems)
	Dim i

	IsLoaded = true
	repDict.REmoveAll

	For i = 1 To repelems.count
		Addreport i, repelems.Item(i)
		With repelems.Item(i)
			repDict.Add .ID, repelems.Item(i)
		end With
	Next
	
end sub

sub Addreport(Row, report)
	Dim NewRow
	Dim NewCell
	
	Set NewRow = Reports.insertrow(Row)
	Set NewCell = NewRow.InsertCell(0)
	Reports.Rows(NewRow.RowIndex).Cells(0).InnerHTML = "<a href='miscs.htm' onclick='vbscript:reportstart(" & report.ID & ")'>" & "<IMG alt='' border='0' src='images\SendChat.gif'></a>"
	Set NewCell = NewRow.InsertCell(1)
	Reports.Rows(NewRow.RowIndex).Cells(NewCell.CellIndex).InnerHTML = "<a href='miscs.htm' onclick='vbscript:reportstart(" & report.ID & ")'>" & report.name & "</a>"
end sub

Sub LoadProps(MscID)
	Dim Msc
	
	Set Msc = wa.Misc(MscID)

	With Msc 
		MscNo.innerHTML = "<b>" & .MscNo & "</b>"
		
		IF .RefKind = 0 then
			AddMiscProps Msc
		Else
			AddRefProps .RefKind, .RefFolderID
		end if
	end With
end Sub

Sub AddMiscProps(Msc)
end Sub

Sub AddRefProps(refType, RefRootID)
	Dim NewRow
	Dim NewCell
	
	Set NewRow = tblProps.insertrow(3)
	Set NewCell = NewRow.InsertCell(0)
	tblProps.Rows(NewRow.RowIndex).Cells(0).InnerHTML = "Ссылка"
	Set NewCell = NewRow.InsertCell(1)

	With tblProps.Rows(NewRow.RowIndex).Cells(1)
		If refType = 3 then 
			.InnerHTML = "<b><a onclick='vbscript:moveagent(0)' href=agents.htm>корреспонденты</a></b>"
		Else
			.InnerHTML = "<b><a onclick='vbscript:moveent(0)' href=entities.htm>объекты учета</a></b>"
		end If
	end With
	
	Set NewRow = tblProps.insertrow(4)
	Set NewCell = NewRow.InsertCell(0)
	tblProps.Rows(NewRow.RowIndex).Cells(0).InnerHTML = "Значение"
	
	Set NewCell = NewRow.InsertCell(1)
	
	With tblProps.Rows(NewRow.RowIndex).Cells(1)
		If RefRootID = 0 then
			If refType = 3 then 
				.InnerHTML = "<b><a onclick='vbscript:moveagent(0)' href=agents.htm>корень</a></b>"
			Else
				.InnerHTML = "<b><a onclick='vbscript:moveent(0)' href=entities.htm>корень</a></b>"
			end If
		Else
			If refType = 3 then 
				.InnerHTML = "<b><a onclick='vbscript:moveagent(" & RefRootID & ")' href=agents.htm>" & wa.agent(RefRootID).Name & "</a></b>"
			Else
				.InnerHTML = "<b><a onclick='vbscript:moveent(" & RefRootID & ")' href=entities.htm>" & wa.entity(RefRootID).Name & "</a></b>"
			end If
		end If
	end With
	
end Sub

function moveagent(ID)
	wa.application.global.Hyperlink.Navigate "", "Main!Agent=" & ID
end function

function moveent(ID)
	wa.application.global.Hyperlink.Navigate "", "Main!Entity=" & ID
end function

function showtable(Mode)
	Dim TableNo, OnOff
	
	TableNo = Int(Mode / 10)
	OnOff = Mode - TableNo * 10
	
	select Case TableNo
		Case 1
			If OnOff = 0 then
				tblProps.style.display = "none"
				propspicture.innerhtml = "<a onclick='vbscript:showtable(11)'><IMG alt='' src='images\down-arrow-green.GIF'>"
			else
				tblProps.style.display = ""
				propspicture.innerhtml = "<a onclick='vbscript:showtable(10)'><IMG alt='' src='images\up-arrow-green.GIF'>"
			end if
			wa.map("tblPropsVisible") = TableNo * 10 + OnOff
			
		Case 3
			If OnOff = 0 then
				reports.style.display = "none"
				reportspicture.innerhtml = "<a onclick='vbscript:showtable(31)'><IMG alt='' src='images\down-arrow-green.GIF'>"
			else
				reports.style.display = ""
				reportspicture.innerhtml = "<a onclick='vbscript:showtable(30)'><IMG alt='' src='images\up-arrow-green.GIF'>"
			end if
			wa.map("tblReportsVisible") = TableNo * 10 + OnOff

	end select
end function

function reportstart(ID)
	RepDict.Item(ID).RunReport
end function

			</SCRIPT>
	</head>
	<body onload="vbscript:window_onload()">
		<TABLE id="Table2" height="183" cellSpacing="1" cellPadding="1" width="224" border="0">
			<TR>
				<TD width="37">
					<H3 align="left"><IMG alt="" src="images\miscs.bmp"></H3>
				</TD>
				<TD>
					<H3 align="left">Аналитика "Разное"</H3>
				</TD>
			</TR>
			<TR>
				<TD colSpan="2">
					<P>
						<HR width="100%" SIZE="1">
						В этом режиме работы отображается дерево произвольных аналитических признаков.<BR>
						В <STRONG>Информационном окне</STRONG> отображаются только те документы, в 
						которых есть ссылка на текущий элемент произвольной аналитики.</P>
				</TD>
			</TR>
			<TR>
				<TD align="left" colSpan="2">
					<HR width="100%" SIZE="1">
				</TD>
			</TR>
		</TABLE>
		<TABLE id="Table3" height="21" cellSpacing="3" cellPadding="0" width="224" bgColor="#009900"
			border="0">
			<TR>
				<TD id="propspicture" width="20"><A onclick="vbscript:showtable(11)"><IMG id="tblPropsShow" alt="" src="images\up-arrow-green.GIF"></A></TD>
				<TD width="100%">
					<P><FONT color="#ffffff" size="2"><STRONG>Свойства</STRONG></FONT></P>
				</TD>
			</TR>
		</TABLE>
		<TABLE id="tblProps" height="104" cellSpacing="1" cellPadding="1" width="224" border="0">
			<TR>
				<TD colSpan="2" id="MscType">Текущая папка:</TD>
			</TR>
			<TR>
				<TD colSpan="2" height="53">
					<H4 id="MscName">наименование папки документов
					</H4>
				</TD>
			</TR>
			<TR>
				<TD>#&nbsp;аналитики</TD>
				<TD id="MscNo"></TD>
			</TR>
			<TR>
				<TD></TD>
				<TD></TD>
			</TR>
		</TABLE>
		<HR align="left" width="224" SIZE="1">
		<TABLE id="tblReps" height="27" cellSpacing="3" cellPadding="0" width="224" bgColor="#009900"
			border="0">
			<TR>
				<TD id="reportspicture" width="20"><A onclick="vbscript:showtable(31)"><IMG id="tblReports" alt="" src="images\up-arrow-green.GIF"></A></TD>
				<TD width="100%">
					<P><FONT color="#ffffff" size="2"><STRONG>Отчеты</STRONG></FONT></P>
				</TD>
			</TR>
		</TABLE>
		<TABLE id="reports" height="20" cellSpacing="1" cellPadding="1" width="224" border="0">
			<TR>
				<TD width="23"></TD>
				<TD></TD>
			</TR>
		</TABLE>
		<HR align="left" width="224" SIZE="1">
	</body>
</html>

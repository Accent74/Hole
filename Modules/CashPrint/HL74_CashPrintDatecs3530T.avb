Option Explicit

' 18/12/2018	- добавил возможность печатать данные по цене и сумме из параметров. для печати цен со скидкой

'	prmPrinter
'	0	- 	Msc
'	1 	-	кассир
'	2	-	пароль кассира
'	3 	-	прочее, если нужно

'	prmData для печати чеков
'	0 	-	операция
'	1	-	номер проводки
'	2	-	тип платежа
'	3 -	использовать свойства или параметры для цены/суммы

'	prmData для печати отчетов 
'	0	-	вид отчета
'	1	-	тип отчета (сокращенный или развернутый)
'	2	-	выводить полный период
'	3	-	начало периода
'	4	-	конец периода
'	5	-	начальный номер
'	6	-	конечный номер

Const MSG_ERROR_PAYMENT 	= "Ошибка оплаты фискального чека"
Const MSG_ERROR_CASHIER 	= "Ошибка регистрации кассира"
Const MSG_ERROR_PRINTROW 	= "Ошибка печати строки фискального чека"
Const MSG_ERROR_NO_REPORT	= "Отчет данным ЭККА не поддерживается"
Const MSG_ERROR_PRN_OPEN 	= "Ошибка доступа к принтеру"
Const MSG_ERROR_NO_COM 		= "Ошибка создания OLE/COM объекта"
Const MSG_ERROR_PRINT_OUT = "Ошибка печати чека выемки денег"
Const MSG_ERROR_PRINT_IN = "Ошибка печати чека внесения денег"
Const MSG_ERROR_OPEN_DRAWER = "Ошибка открытия денежного ящика"
Const MSG_ERROR_RESET_CHECK = "Ошибка аннулирования чека"
Const MSG_ERROR_NULL_CHECK_PRINT = "Ошибка печати нулевого чека"
Const MSG_ERROR_PRINT_ERPORT = "Ошибка печати отчета"
Const MSG_ERROR_OPEN_CHECK = "Ошибка открытия фискального чека"
Const MSG_ERROR_PROGRAM = "Ошибка программирования артикула: "

Const MSG_NO_SUPPPORT = "Функция для данного принтера не поддерживается"
Const MSG_BOX_CAPTION = "Фискальный регистратор"

Const prmEntCashPrnName = "Наименование для ЭККА"

'---
'
'---
Sub PrintCash_DoPrintCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	If IsNumeric(Msc.String2) Then
		Set CashPrn = PrintCash_Open(CLng(Msc.String2), Msc.Long1, prmPrinter)

		If Not CashPrn Is Nothing Then
			PrintCash_PrintCheck CashPrn, prmData
			PrintCash_Close CashPrn, CLng(Msc.String2)
			Set CashPrn = Nothing
		End If
	Else
		MsgBox "Номер порта должен быть числом", vbExclamation, MSG_BOX_CAPTION
	End If
End Sub
'---
'
'---
Sub PrintCash_DoPrintCheckRet(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	If IsNumeric(Msc.String2) Then
		Set CashPrn = PrintCash_Open(CLng(Msc.String2), Msc.Long1, prmPrinter)

		If Not CashPrn Is Nothing Then
			PrintCash_PrintCheckRet CashPrn, prmData
			PrintCash_Close CashPrn, CLng(Msc.String2)
			Set CashPrn = Nothing
		End If
	Else
		MsgBox "Номер порта должен быть числом", vbExclamation, MSG_BOX_CAPTION
	End If

End Sub

'---
'
'---
Function PrintCash_Open(PortNo, BoudRate, prmPrinter)
	Dim CashPrn, CashierName, Passw, Result

	CashierName = prmPrinter(1)
	Passw = 0

	On Error Resume Next

	Set PrintCash_Open = Nothing
	Set CashPrn = CreateObject("DatecsECR.TECRFisc")

	If Err.Number = 0 Then
		Result = CashPrn.SetComPort(PortNo, BoudRate)
		If Result = 1 Then
			Set PrintCash_Open = CashPrn
		Else
			MsgBox MSG_ERROR_PRN_OPEN & vbNewLine & " #" & Result, vbCritical, MSG_BOX_CAPTION
		End If
	Else
		MsgBox MSG_ERROR_NO_COM & vbNewLine & Err.description & " (" & Err.Number & ")", vbCritical, MSG_BOX_CAPTION
	End If

End Function
'---
'
'---
Function PrintCash_PrintCheck(CashPrn, ByRef prm)
	Dim Op, TrNo, TrList, DepName, PaymentType
	Dim OneLinePrm

	PrintCash_PrintCheck = True

	Set Op = prm(0)
	TrNo = prm(1)
	PaymentType = 3	'prm(2)
	OneLinePrm = Array(CashPrn, prm(3))

	Set TrList = Op.TransList(TrNo)

	DepName = TrList.AgFromBind.Name

	If CashPrn.OpenFiscReceipt(1, 1, "0000") Then
		TrList.EnumTrans GetRef("PrintCash_PrintOneLine"), OneLinePrm

		If Not CashPrn.Payment(TrList.Sum, PaymentType) Then
			MsgBox MSG_ERROR_PAYMENT & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
			PrintCash_PrintCheck = False
		End If
	Else
		MsgBox MSG_ERROR_OPEN_CHECK & " : " & CashPrn.GetError, vbExclamation, MSG_BOX_CAPTION
	End If

End Function
'---
'
'---
Function PrintCash_PrintCheckRet(CashPrn, ByRef prm)
	Dim Op, TrNo, TrList, DepName, PaymentType
	Dim OneLinePrm

	PrintCash_PrintCheckRet = True

	Set Op = prm(0)
	TrNo = prm(1)
	PaymentType = 3	'prm(2)
	OneLinePrm = Array(CashPrn, prm(3))

	Set TrList = Op.TransList(TrNo)

	DepName = TrList.AgFromBind.Name

	If CashPrn.OpenRefundReceipt(1, 1, "0000") Then
		TrList.EnumTrans GetRef("PrintCash_PrintOneLineRet"), OneLinePrm

		If Not CashPrn.Payment(TrList.Sum, PaymentType) Then
			MsgBox MSG_ERROR_PAYMENT, vbCritical, MSG_BOX_CAPTION
			PrintCash_PrintCheckRet = False
		End If
	End If
End Function
'---
'
'---
Function PrintCash_PrintOneLineRet(Tr, ByRef OneLinePrm)
	Dim EntName, CashPrn

	Set CashPrn = OneLinePrm(0)

	EntName = Tr.EntBind.ParamString(prmEntCashPrnName)
	If EntName = "" Then EntName = Tr.EntBind.Name

	If CashPrn.SellArtCashDS(False, CLng(Tr.Art), Tr.Qty, 0, 0) Then
		PrintCash_PrintOneLineRet = True
	Else
		MsgBox MSG_ERROR_PRINTROW & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		PrintCash_PrintOneLineRet = False
	End If
End Function

'---
'
'---
Function PrintCash_PrintOneLine(Tr, ByRef OneLinePrm)
	Dim EntName, ArtInfo, ArtNo, CashPrn, IsDisc

	Set CashPrn = OneLinePrm(0)
	IsDisc = OneLinePrm(1)

	EntName = Tr.EntBind.ParamString(prmEntCashPrnName)
	ArtNo = Tr.EntBind.Art
	If EntName = "" Then EntName = Tr.EntBind.Name

	ArtInfo = CashPrn.GetArtInfo(CLng(ArtNo))

	If ArtInfo = "" Then 
		If Not CashPrn.ProgramArt( _
				Asc(com_getparamvalue(Tr.Entity, prmEntTaxGroup, "А")), _
				CLng(ArtNo), _
				com_getparamvalue(Tr.Entity, prmGroupNo, 1), _
				iif(IsDisc, Tr.Params(prmTrPrice).Value2, Tr.Price), _
				"0000", _
				com_getparamvalue(Tr.Entity, prmEntCashName, Tr.EntBind.Name)) Then
			MsgBox "Ошибка программирования артикула: " & CashPrn.GetError
			PrintCash_PrintOneLine = False
			Exit Function
		End If
	End If

	If CashPrn.SellArtCashDS(True, CLng(ArtNo), Tr.Qty, 0, 0) Then
		PrintCash_PrintOneLine = True
	Else
		MsgBox MSG_ERROR_PRINTROW & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		PrintCash_PrintOneLine = False
	End If
End Function
'---
'
'---
Function PrintCash_Close(CashPrn, PortNo)
	PrintCash_Close = (CashPrn.CloseComPort(PortNo) = 1)
End Function
'---
'
'	prmData для печати чеков
'	0	-	вид отчета
'	1	-	тип отчета (сокращенный или развернутый)
'	2	-	выводить полный период
'	3	-	начало периода
'	4	-	конец периода
'	5	-	начальный номер
'	6	-	конечный номер
'
'---
Sub PrintCash_PrintReports(prm)
	Dim CashPrn, prmPrinter, prmData, Msc, IsErr

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)
	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	IsErr = False

	If Not CashPrn Is Nothing Then
		
		Select Case prmData(0)
			Case 1	'	x-report
				IsErr = CashPrn.XReport("0000")
	
			Case 2	'	z-report
				IsErr = CashPrn.ZReport("0000")
	
			Case 3	'	report by art
				IsErr = CashPrn.ArticulsReports(0, "0000")
	
			Case 4	'	report by disount
				MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
	
			Case 5	'	report by Date
				If prmData(1) Then
					IsErr = CashPrn.PeriodRepByDate("0000", Replace(formatdate2(prmData(3), "dd.mm.yy"), ".", ""), Replace(formatdate2(prmData(4), "dd.mm.yy"), ".", ""))
				Else
					IsErr = CashPrn.PeriodRepByDateFull("0000", Replace(formatdate2(prmData(4), "dd.mm.yy"), ".", ""), Replace(formatdate2(prmData(4), "dd.mm.yy"), ".", ""))
				End If
	
			Case 6	'	report by no
				If CLng(prmData(5)) = 0 Or CLng(prmData(6)) = 0 Then Exit Sub

				If prmData(1) Then
					IsErr = CashPrn.PeriodRepByNumb("0000", CLng(prmData(5)), CLng(prmData(6)))
				Else
					IsErr = CashPrn.PeriodRepByNumbFull("0000", CLng(prmData(5)), CLng(prmData(6)))
				End If

			Case Else
				MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
	
		End Select 

		If Not IsErr Then
			MsgBox MSG_ERROR_PRINT_ERPORT & ":" & CashPrn.GetError, vbExclamation, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)
	End If
End Sub
'---
'
'---
Sub PrintCash_PrintNullCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		If Not CashPrn.NullReceipt (1, 1, "0000") Then
			MsgBox MSG_ERROR_NULL_CHECK_PRINT & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		End If
	End If

End Sub
'---
'
'---
Sub PrintCash_PrintCheckOut(prm)
	Dim CashPrn, prmPrinter, prmData, Msc
	Dim Op, Sum

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		Set Op = prmData(0)
		Sum = CashPrn.ServiceOut
		CashPrn.ServiceOut = Op.Sum
		If (CashPrn.ServiceOut + Sum) <> Op.Sum Then
			MsgBox MSG_ERROR_PRINT_OUT & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		End If
	End If

End Sub
'---
'
'---
Sub PrintCash_PrintCheckIn(prm)
	Dim CashPrn, prmPrinter, prmData, Msc
	Dim Op, Sum

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		Set Op = prmData(0)
		Sum = CashPrn.ServiceIn
		CashPrn.ServiceIn = Op.Sum
		If (CashPrn.ServiceIn - Sum) <> Op.Sum Then
			MsgBox MSG_ERROR_PRINT_IN & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		End If
	End If

End Sub
'---
'
'---
Sub PrintCash_OpenCashDrawer(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		If Not CashPrn.OpenDrawer(20) Then
			MsgBox MSG_ERROR_OPEN_DRAWER & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		End If
	End If
End Sub
'---
'
'---
Sub PrintCash_ResetCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		If Not CashPrn.ReceiptCancel Then
			MsgBox MSG_ERROR_RESET_CHECK & " Ошибка:" & CashPrn.GetError, vbCritical, MSG_BOX_CAPTION
		End If
	End If

End Sub
'---
'
'---


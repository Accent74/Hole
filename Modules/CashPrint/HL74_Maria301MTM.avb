'#include "HL74_Common.avb"

' модуль для работы с Мария 301МТМ
' использует Resonance OLE Manager 2018
'
Option Explicit

'	prmPrinter
'	0	- 	Msc
'	1 	-	кассир
'	2	-	пароль кассира
'	3 	-	прочее, если нужно

'	prmData для печати чеков
'	0 	-	операция
'	1	-	номер проводки
'	2	-	тип платежа

'	prmData для печати отчетов 
'	0	-	вид отчета
'	1	-	тип отчета (сокращенный или развернутый)
'	2	-	выводить полный период
'	3	-	начало периода
'	4	-	конец периода
'	5	-	начальный номер
'	6	-	конечный номер

Const MSG_ERROR_NO_REPORT = "Отчет данным ЭККА не поддерживается"
Const MSG_ERROR_PRN_REPORT = "Отчет печати отчета"

Const MSG_ERROR_PAYMENT 		= "Ошибка оплаты фискального чека"
Const MSG_ERROR_RET 				= "Ошибка чека возврата"
Const MSG_ERROR_CASHIER 		= "Ошибка регистрации кассира"
Const MSG_ERROR_PRINTROW 		= "Ошибка печати строки фискального чека"
Const MSG_ERROR_PRN_OPEN 		= "Ошибка доступа к принтеру"
Const MSG_ERROR_NO_COM 			= "Ошибка создания OLE/COM объекта"
Const MSG_ERROR_PRINT_OUT 		= "Ошибка печати чека выемки денег"
Const MSG_ERROR_PRINT_IN	 	= "Ошибка печати чека внесения денег"
Const MSG_ERROR_OPEN_DRAWER	= "Ошибка открытия денежного ящика"
Const MSG_ERROR_RESET_CHECK 	= "Ошибка аннулирования чека"
Const MSG_ERROR_CLOSE_CHECK 	= "Ошибка закрытия чека"

Const MSG_NO_SUPPPORT = "Функция для данного принтера не поддерживается"
Const MSG_BOX_CAPTION = "Печать фискального чека"

Const prmEntCashPrnName = "Наименование для ЭККА"
Const prmTrPrice = "Цена"

Const DEF_PASSWORD = "1111111111"

Const TAX_1			= 1
Const TAX_2			= 2
Const TAX_3			= 3
Const TAX_4			= 4
Const TAX_5			= 5
Const TAX_6			= 6
Const TAX_7			= 7
Const TAX_8			= 8
Const TAX_9			= 9
Const TAX_NDS			= 10
Const TAX_NOTNDS		= 11
Const TAX_NOTAX		= 12

Const SALE_RECEIPT		= 0
Const REFUND_RECEIPT		= 1

'---
'
'---
Sub PrintCash_DoPrintCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	If IsNumeric(Msc.String2) Then
		Set CashPrn = PrintCash_Open(CLng(Msc.String2), Msc.Long1, prmPrinter)

		If Not CashPrn Is Nothing Then
			PrintCash_PrintCheck CashPrn, prmData
			PrintCash_Close CashPrn, CLng(Msc.String2)
		End If
	Else
		MsgBox "Номер порта должен быть числом", vbExclamation, MSG_BOX_CAPTION
	End If
End Sub
'---
'
'---
Sub PrintCash_DoPrintCheckRet(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	If IsNumeric(Msc.String2) Then

		Set CashPrn = PrintCash_Open(CLng(Msc.String2), Msc.Long1, prmPrinter)

		If Not CashPrn Is Nothing Then
			PrintCash_PrintCheckRet CashPrn, prmData
			PrintCash_Close CashPrn, CLng(Msc.String2)
		End If
	Else
		MsgBox "Номер порта должен быть числом", vbExclamation, MSG_BOX_CAPTION
	End If

End Sub
'---
'
'---
Function PrintCash_GetBoudRate(BoudRate)
	Select Case BoudRate
		Case 4800
			PrintCash_GetBoudRate = 1
		Case 9600
			PrintCash_GetBoudRate = 2
		Case 19200
			PrintCash_GetBoudRate = 3
		Case 38400
			PrintCash_GetBoudRate = 4
		Case 57600
			PrintCash_GetBoudRate = 5
		Case 115200
			PrintCash_GetBoudRate = 6
		Case Else
			PrintCash_GetBoudRate = 0
	End Select
End Function
'---
'
'---
Function CreateOLEObject(OLEName)
	On Error Resume Next
	Set CreateOLEObject = CreateObject("M304Manager.Application")

	If Err.Number <> 0 Then	
		MsgBox MSG_ERROR_NO_COM & vbNewLine & Err.description & " (" & Err.Number & ")", vbCritical, MSG_BOX_CAPTION
		Set CreateOLEObject = Nothing
	End If

	Err.Clear
End Function
'---
'
'---
Function PrintCash_Open(PortNo, BoudRate, prmPrinter)
	Dim CashPrn, CashierName, Passw, Result

	CashierName = prmPrinter(1)
	Passw = iIf(prmPrinter(2) = 0, DEF_PASSWORD, prmPrinter(2))

	Set CashPrn = CreateOLEObject("M304Manager.Application")
	Set PrintCash_Open = Nothing

	If Not CashPrn Is Nothing Then
		If PortNo = 0 Then
			Result = CashPrn.InitAuto(CashierName, Passw)
		Else
			Result = CashPrn.Init(PortNo, CashierName, Passw)
		End If

		If Result = 1 Then
			Set PrintCash_Open = CashPrn
		Else
			MsgBox MSG_ERROR_PRN_OPEN & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If
	End If

End Function
'---
'
'---
Function PrintCash_PrintCheck(CashPrn, ByRef prm)
	Dim Op, TrNo, TrList, DepName, PaymentType
	Dim OneLinePrm, Result

	PrintCash_PrintCheck = True

	Set Op = prm(0)
	TrNo = prm(1)
	PaymentType = 1	'prm(2)
	OneLinePrm = Array(CashPrn, prm(3))

	Set TrList = Op.TransList(TrNo)
	DepName = TrList.AgFromBind.Name

	Result = 0

	If CashPrn.OpenCheck() = 1 Then
		TrList.EnumTrans GetRef("PrintCash_PrintOneLine"), OneLinePrm

		If IsEmpty(CashPrn.LastErrorCode) Then
			Result = CashPrn.AddPayment(CLng(TrList.Sum * 100), PaymentType - 1)

			If Result = 1 Then 
				If CashPrn.CloseCheck() = 0 Then
					MsgBox MSG_ERROR_CLOSE_CHECK & vbNewLine & CashPrn.LastErrorMessage(), vbCritical, MSG_BOX_CAPTION
					Result = 0
				Else
					Op.DocNo = CashPrn.LastCheckNumber
				End If
			Else
				MsgBox MSG_ERROR_PAYMENT & vbNewLine & CashPrn.LastErrorMessage(), vbCritical, MSG_BOX_CAPTION
			End If
		Else
			MsgBox MSG_ERROR_PRINTROW & vbNewLine & CashPrn.LastErrorMessage(), vbCritical, MSG_BOX_CAPTION
		End If
	End If

	PrintCash_PrintCheck = (Result = 1)

End Function
'---
'
'---
Function PrintCash_PrintOneLine(Tr, ByRef OneLinePrm)
	Dim EntName, CashPrn, IsDisc

	Set CashPrn = OneLinePrm(0)
	IsDisc = OneLinePrm(1)

	EntName = Tr.EntBind.ParamString(prmEntCashPrnName)
	If EntName = "" Then EntName = Tr.EntBind.Name

	PrintCash_PrintOneLine = True
	If Tr.EntID = 0 Or Tr.Price = 0 Or Tr.Qty = 0 Then Exit Function
	
	If IsDisc Then
		' Продажа товара со скидкой : название, количество, цена (в копейках), делимость, налог1, налог2, код артикула, направление, название, сумма
		If Tr.EntBind.Art = "" Or Not IsNumeric(Tr.EntBind.Art) Then
			PrintCash_PrintOneLine = (CashPrn.FiscalLineEx(EntName, Tr.Qty, CLng(Tr.Params(prmTrPrice).Value2 * 100), 0, 0, 0, _
				1, 0, "Скидка", _
							 CLng(Tr.Params(prmTrSum).Value2 * 100)) <> 0)
		Else
			PrintCash_PrintOneLine = (CashPrn.FiscalLineEx(EntName, Tr.Qty, CLng(Tr.Params(prmTrPrice).Value2 * 100), 0, 0, 0, _
				Tr.EntBind.Art, 0, "Скидка", _
					 CLng(Tr.Params(prmTrSum).Value2 * 100)) <> 0)
		End If
	Else
		' Продажа товара: название, количество, цена (в копейках), делимость, налог1, налог2, код артикула

		If Trim(Tr.EntBind.Art) = "" Then
			PrintCash_PrintOneLine = (CashPrn.FiscalLine(EntName, Tr.Qty, CLng(Tr.Price * 100), 0, 0, 0, Tr.EntID) <> 0)
		Else
			PrintCash_PrintOneLine = (CashPrn.FiscalLine(EntName, Tr.Qty, CLng(Tr.Price * 100), 0, 0, 0, Tr.EntBind.Art) <> 0)
		End If
	End If

End Function

'---
'
'---
Function PrintCash_PrintCheckRet(CashPrn, ByRef prm)
	Dim Op, TrNo, TrList, DepName, PaymentType
	Dim OneLinePrm, Result

	PrintCash_PrintCheckRet = True

	Set Op = prm(0)
	TrNo = prm(1)
	PaymentType = 1	'prm(2)
	OneLinePrm = Array(CashPrn, prm(3))

	Set TrList = Op.TransList(TrNo)
	DepName = TrList.AgFromBind.Name

	Result = 0

	If CashPrn.SetReturnCheckNumberStr(Op.DocNo) = 1 Then
		If CashPrn.OpenReturnCheck() = 1 Then
			TrList.EnumTrans GetRef("PrintCash_PrintOneLineRet"), OneLinePrm

			If IsEmpty(CashPrn.LastErrorCode) Then
				If CashPrn.CloseCheck() = 0 Then
					MsgBox MSG_ERROR_CLOSE_CHECK & vbNewLine & CashPrn.LastErrorMessage(), vbCritical, MSG_BOX_CAPTION
					Result = 0
				End If
			Else
				MsgBox MSG_ERROR_PRINTROW & vbNewLine & CashPrn.LastErrorMessage(), vbCritical, MSG_BOX_CAPTION
			End If
		End If
	End If

	PrintCash_PrintCheckRet = (Result = 1)
End Function
'---
'
'---
Function PrintCash_PrintOneLineRet(Tr, ByRef OneLinePrm)
	Dim EntName, CashPrn

	Set CashPrn = OneLinePrm(0)

	EntName = Tr.EntBind.ParamString(prmEntCashPrnName)
	If EntName = "" Then EntName = Tr.EntBind.Name

	PrintCash_PrintOneLineRet = True

	' Продажа товара: название, количество, цена (в копейках), делимость, налог1, налог2, код артикула
	If Tr.EntBind.Art = "" Then
		PrintCash_PrintOneLineRet = (CashPrn.FiscalLine(EntName, Tr.Qty, CLng(Tr.Price * 100), 0, 0, 0,1) <> 0)
	Else
		PrintCash_PrintOneLineRet = (CashPrn.FiscalLine(EntName, Tr.Qty, CLng(Tr.Price * 100), 0, 0, 0, Tr.EntBind.Art) <> 0)
	End If
End Function

'---
'
'---

Function PrintCash_Close(CashPrn, PortNo)
	PrintCash_Close = (CashPrn.Done() = 1)
	Set CashPrn = Nothing
End Function
'---
'
'	prmData для печати чеков
'	0	-	вид отчета
'	1	-	тип отчета (сокращенный или развернутый)
'	2	-	выводить полный период
'	3	-	начало периода
'	4	-	конец периода
'	5	-	начальный номер
'	6	-	конечный номер
'
'---
Sub PrintCash_PrintReports(prm)
	Dim CashPrn, prmPrinter, prmData, Msc, IsErr, dStart, dEnd

	prmPrinter = prm(0)
	prmData = prm(1)
	IsErr = 1

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		
		Select Case prmData(0)
			Case 1	'	x-report
				IsErr = CashPrn.XReport()
	
			Case 2	'	z-report
				IsErr = CashPrn.ZReport()
	
			Case 3	'	report by art
'				If Not prmData(1) Then
					' краткий вариант
					IsErr = CashPrn.ArticleReport
'				Else
'					MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
'				End If
	
			Case 4	'	report by disount
'				If Not prmData(1) Then
					' краткий вариант
					IsErr = CashPrn.DiscountReport()
'				Else
'					MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
'				End If
			
			Case 5	'	report by Date
				dStart = prmData(3)
				dEnd =  prmData(4)

'				If Not prmData(1) Then
					' краткий вариант
					IsErr = CashPrn.PeriodicalFiscalReportDate(dStart, dEnd)
'				Else
'					MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
'				End If
	
			Case 6	'	report by no
'				If Not prmData(1) Then
					' краткий вариант
					IsErr = CashPrn.PeriodicalFiscalReport(prmData(5), prmData(6))
'				Else
'					MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
'				End If

			Case Else
				MsgBox MSG_NO_SUPPPORT, vbInformation, MSG_BOX_CAPTION
	
		End Select 

		If IsErr <> 1 Then
			MsgBox MSG_ERROR_PRN_REPORT & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)
	End If
End Sub
'---
'
'---
Sub PrintCash_PrintNullCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc
	Dim Op

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		If CashPrn.NullCheck() <> 1 Then
			MsgBox MSG_ERROR_PRN_REPORT & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)
	End If

End Sub
'---
'
'---
Sub PrintCash_PrintCheckOut(prm)
	Dim CashPrn, prmPrinter, prmData, Msc
	Dim Op

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		Set Op = prmData(0)
	
		If CashPrn.MoveCash(0, CLng(Op.Sum * 100)) <> 1 Then
			MsgBox MSG_ERROR_PRINT_OUT & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)

	End If

End Sub
'---
'
'---
Sub PrintCash_PrintCheckIn(prm)
	Dim CashPrn, prmPrinter, prmData, Msc
	Dim Op

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		Set Op = prmData(0)

		If CashPrn.MoveCash(1, CLng(Op.Sum * 100)) <> 1 Then
			MsgBox MSG_ERROR_PRINT_IN & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)

	End If

End Sub
'---
'
'---
Sub PrintCash_OpenCashDrawer(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then
		
		If CashPrn.OpenCashBox() <> 1 Then
			MsgBox MSG_ERROR_OPEN_DRAWER & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)

	End If
End Sub
'---
'
'---
Function PrintCash_ResetCheck(prm)
	Dim CashPrn, prmPrinter, prmData, Msc

	prmPrinter = prm(0)
	prmData = prm(1)

	Set Msc = prmPrinter(0)

	Set CashPrn = PrintCash_Open(Msc.String2, Msc.Long1, prmPrinter)

	If Not CashPrn Is Nothing Then

		If CashPrn.AbortCheck() <> 1 Then
			MsgBox MSG_ERROR_RESET_CHECK & vbNewLine & CashPrn.LastErrorMessage, vbCritical, MSG_BOX_CAPTION
		End If

		PrintCash_Close CashPrn, CLng(Msc.String2)

	End If

End Function
'---
'
'---


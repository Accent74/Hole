'#include "HL74_ADO.avb"
'#include "HL74_Common.avb"
'#include "HL74_const.avb"

Option Explicit

Sub HL74_RDLAddColumns
	Dim SQL, aData
	Dim RepKind 	' Тип отчета РЕЕСТР ДОКУМЕНТОВ
	Dim RDL, Book	' Объект RepDocList и его Книга(Excel)

	If Not com_GetLogicParamValue(Workarea, prmDBExtRepWizard, False) Then Exit Sub

	RepKind = WorkArea.Map("RepKind")
	If RepKind <> 10 Then Exit Sub

	Set RDL = WorkArea.CreateReport("RepDocList")
	RDL.Kind = WorkArea.Map("Kind")
	RDL.KindID = WorkArea.Map("KindID")
	Workarea.Map("Result") = True

	If RDL.Build Then
		Set Book = RDL.MakeSheet
		Book.Sheet1.PageSetup.Orientation = 2
		SQL = "Select Rep_Item_Attr_Name From REP_ITEMS_ATTR Where Rep_Kind = 10"
		If	GetRs2(SQL, aData) Then	CopyColumns Book, Book.Sheet1, 6, aData
		Insert_Values RDL, Book
		Book.Sheet1.AutoFit 2
	End If
End Sub

'--------------------------------------------------------------------------------------------------
' Добавим необходимое количество колонок
'--------------------------------------------------------------------------------------------------
Sub CopyColumns (Book, Sh, Col, aName)
	Dim i, j
	For i = 0 To UBound(aName, 2)
		Sh.Activate
		Sh.Range(Col, 4, Col, Sh.Rows).Select 
		Book.Copy
		Sh.InsertColumn(Col+i+1)
		Sh.Cell(4,Col+i+1).Select
		Book.Paste
		Sh.Cell(4,Col+i+1).Value = aName(0, i)
		For j = 5 To Sh.Rows
			Sh.Cell(j,Col+i+1).Value = ""
		Next
	Next
End Sub

'--------------------------------------------------------------------------------------------------
' Определение новых параметров, которые как "свойства операции"
'--------------------------------------------------------------------------------------------------
Sub Props_Init (Props)
	Set Props = CreateLibObject("Map")
	Props("Шаблон документа") = "1"
	Props("Форма док-та (полное наим.)") = "2"
	Props("Состояние документа") = "3"
	Props("Дата последней модификации") = "4"
	Props("Первый текстовый параметр") = "String1"
	Props("Второй текстовый параметр") = "String2"
	Props("Третий текстовый параметр") = "String3"
	Props("Первый параметр суммы") = "Sum1"
	Props("Второй параметр суммы") = "Sum2"
	Props("Третий параметр суммы") = "Sum3"
	Props("Первый параметр даты") = "Date1"
	Props("Второй параметр даты") = "Date2"
	Props("Третий параметр даты") = "Date3"
	Props("Первый числовой параметр") = "Long1"
	Props("Второй числовой параметр") = "Long2"
	Props("Третий числовой параметр") = "Long3"
End Sub

'--------------------------------------------------------------------------------------------------
' Поиск ЗНАЧЕНИЙ некоторых из новых параметров (из тех, которые как "свойства операции")
'--------------------------------------------------------------------------------------------------
Sub Props_Find (ID, Prop, Value)
	Dim SQL, aData, FormID

	Select Case Prop

		Case "1"
				Value = WorkArea.Template(WorkArea.Operation(ID).TemplateID).Name

		Case "2"
				FormID = WorkArea.Operation(ID).FormID
				SQL = "Select FRM_NAME From FORMS Where FRM_ID = " & FormID
				If GetRs2(SQL, aData) Then Value = aData(0,0)

		Case "3"
				SQL = "Select DOC_DONE From DOCUMENTS Where DOC_ID = " & ID
				If GetRs2(SQL, aData) Then Value = aData(0,0)
				Select Case Value
					Case 0 Value = "Не закончен"
					Case 1 Value = "Не проведён"
					Case 2 Value = "Завершен"
					Case 100 Value = "Удален"
					Case 101 Value = "Удален"
					Case 102 Value = "Удален"
				End Select

		Case "4"
				SQL = "Select LAST_DATE From DOCUMENTS Where DOC_ID = " & ID
				If GetRs2(SQL, aData) Then Value = aData(0,0)
	End Select
End Sub

'--------------------------------------------------------------------------------------------------
' Вставка ЗНАЧЕНИЙ новых параметров
'--------------------------------------------------------------------------------------------------
Sub Insert_Values (RDL, Book)
	Dim i, j, SQL, aData
	Dim Props, Value
	Props_Init Props

	For i = 1 To RDL.Count
		SQL = "Select Rep_Item, Rep_Item_Attr_Name From REP_ITEMS_ATTR Where Rep_Kind = 10"
		If	GetRs2(SQL, aData) Then

			For j = 0 To UBound(aData, 2)

				If aData(0,j) = 1 Then
					If Len(Props(aData(1,j))) > 1 Then
						Execute "Book.Sheet1.Cell(5 + " & i & ",7 + " & j & ").Value = WorkArea.Operation(RDL(" & i & ").ID)." & Props(aData(1,j))
					Else
						Props_Find RDL(i).ID, Props(aData(1,j)), Value
						Book.Sheet1.Cell(5+i,7+j).Value = Value
					End If

				Else
					Book.Sheet1.Cell(5+i,7+j).Value = Com_GetParamValueRef(WorkArea.Operation(RDL(i).ID), aData(1,j), "<Не указано>")
				End If
			Next
		End If
	Next
End Sub
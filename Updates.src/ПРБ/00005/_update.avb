'-------------------
'	процедура установки окружения
'	для работы стандартных решений для Акцент 7.0
'	005
'-------------------
Option Explicit

'#include "HL74_ADO.avb"
'#include "HL74_XML.avb"
'#include "HL74_UpdateSolution.avb"
'#include "HL74_Actionmenu_class.avb"

Const solution_name = "ПРБ_00"
Const ver = "005"
Const update_xml_filename = "HL74_update_"

Dim LogFile
'---
'
'---
Sub writelog(aParam, updater)
	Dim Text

	Text = formatdate2(Now, "dd-mm-yy hh:nn") & " " & Join(aParam)

	If LogFile Is Nothing Then
		updater.writelog Text
	Else
		LogFile.Writeline Text
	End If
End Sub
'---
'
'---
Function OpenLogFile(FileName)
	Dim FSO, RD, FullPath, aPath

	Set RD = CreateLibObject("Redirect")
	aPath = RD.GetPathArray("*.aup")

	If UBound(aPath) <> -1 Then
		FullPath = aPath(0)
		If Right(FullPath, 1) <> "\" Then FullPath = FullPath & "\"

		FullPath = FullPath & FileName

		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set OpenLogFile = FSO.OpenTextFile(FullPath, 8, True)
	Else
		Set OpenLogFile = Nothing
	End If

End Function
'---
'
'---
Function Getdbname()
	If App.AppType = "OLEDB" Then
		Getdbname = workarea.filename
	Else
		Getdbname = workarea.daodatabase.name
	End If
End Function
'---
'
'---
Sub Main(Updater)
	Dim Fail

	Set LogFile = OpenLogFile(solution_name & ver & ".log.txt")

	writelog Array(String(10, "-"), "Начало установки обновления", ver), updater
	writelog Array("База данных:", Getdbname(), "пользователь:", workarea.dbuser), updater
	UpdateFromXML update_xml_filename & ver & ".xml", updater

	If Not updater.fail Then
		addcommandmenu updater
	End If

	If Not updater.fail Then
		checkcurrency updater
	End If
	
	If updater.fail Then
		writelog Array(String(10, "-"), "Процесс установки прерван из-за возникшей ошибки"), updater
	Else
		writelog Array(String(10, "-"), "Процесс установки завершен успешно."), updater
	End If

	If Not LogFile Is Nothing Then LogFile.Close

End Sub
'------
'
'------
Sub checkcurrency(updater)
	Dim RurID, RurCur

	RurID = Workarea.Curs.Find("810")

	If RurID <> 0 Then
		Set RurCur = Workarea.Cur(RurID)
		RurCur.Code = "643"
		RurCur.Save
	End If

	If App.AppType = "DAO" Then
		Workarea.DAODataBase.Execute "update currencies set crc_spell='1	руб.	руб.	руб.	0	коп.	коп.	коп.' where crc_code='643'"
		Workarea.DAODataBase.Execute "update currencies set crc_spell='0	грн.	грн.	грн.	0	коп.	коп.	коп.' where crc_code='980'"
	Else
		GetRs "update currencies set crc_spell='1	руб.	руб.	руб.	0	коп.	коп.	коп.' where crc_code='643'"
		GetRs "update currencies set crc_spell='0	грн.	грн.	грн.	0	коп.	коп.	коп.' where crc_code='980'"
	End If

End Sub
'------
'
'------
Sub addcommandmenu(updater)

	AddMenuItem "Сервис", 0, "-", ""	
	AddMenuItem "Сервис", 0, "Замена базовой валюты", "HL74_ChangeBaseCur.adg"	
	writelog Array("В меню по кнопке Действия добавлена команда", "Сервис-Замена базовой валюты"), updater

End Sub
'------
'
'------
Sub AddMenuItem(MenuRootName, MenuItemPos, MenuItemName, MenuItemFile)
	Dim MA
	Dim MA_Item

	Set MA = New mActions
	Set MA_Item = MA.ItemByName(MenuRootName)

	If MA_Item Is Nothing Then
		Set MA_Item = MA.Root.Create(MenuRootName, " ", 0, 0)
	End If

	Set MA_Item = MA_Item.Children.Create(MenuItemName, MenuItemFile, MA_Item.Level + 1, MenuItemPos)
	
	If Not MA_Item Is Nothing Then 
		MA.Save
	End If
	
End Sub
'------
'
'------


